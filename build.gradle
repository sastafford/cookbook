plugins {
  id "com.marklogic.ml-gradle" version "2.6.0"
  id "java"
  id "application"
}

group 'com.marklogic'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()

    // MarkLogic Maven Repository
	maven { url "https://developer.marklogic.com/maven2/" }

    //Spring Maven Repository
    maven { url "http://repo.spring.io/milestone" }
}

dependencies {
    // used for logging purposes
    compile 'org.slf4j:slf4j-simple:1.7.25'

    compile "com.marklogic:marklogic-client-api:4.0.0-EA4"
    compile "com.marklogic:ml-javaclient-util:2.13.0"

    //Spring libraries
    compile "org.springframework.batch:spring-batch-core:3.0.7.RELEASE"
    compile 'org.springframework:spring-context:4.3.8.RELEASE'
    compile "org.springframework:spring-jdbc:4.3.8.RELEASE"

    //HyperSQL Database
    compile "com.h2database:h2:1.4.193"


    // testing libraries
    testCompile "junit:junit:4.12"
    testCompile "org.xmlunit:xmlunit-core:2.3.0"
    testCompile "com.marklogic:ml-junit:2.6.0"

    // Used for running HyperSQL
    runtime "com.h2database:h2:1.4.193"

}

// The main program for the RDBMS migration program
mainClassName = "org.springframework.batch.core.launch.support.CommandLineJobRunner"

// Starts up the HyperSQL Data Manager
task runH2DataManager(type: JavaExec) {
    group = "HSQLDB"
    classpath = configurations.runtime
    main = "org.h2.tools.Console"
    args = [
            "-url", "jdbc:h2:file:./data/sample",
            "-user", "sa"
    ]
}

// Loads H2 database with sample data
task loadH2Data(type: JavaExec) {
    group = "HSQLDB"
    classpath = configurations.runtime
    main = "org.h2.tools.RunScript"
    args = [
            "-url", "jdbc:h2:file:./data/sample",
            "-user", "sa",
            "-script", "./src/test/resources/db/sampledata.sql"
    ]
}